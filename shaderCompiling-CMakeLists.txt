file(GLOB_RECURSE HLSL_SOURCE_FILES
        "shaders/*.frag"
        "shaders/*.vert"
        "shaders/*.comp"
        "shaders/*.mesh"
)

file(CHMOD ${DIRECTX_DXC_TOOL} PERMISSIONS OWNER_EXECUTE OWNER_WRITE OWNER_READ)

foreach (HLSL ${HLSL_SOURCE_FILES})
    get_filename_component(FILE_NAME ${HLSL} NAME)
    if (FILE_NAME MATCHES ".vert$")
        set(SHADER_PROFILE "vs")
    elseif (FILE_NAME MATCHES ".frag$")
        set(SHADER_PROFILE "ps")
    elseif (FILE_NAME MATCHES ".mesh$")
        set(SHADER_PROFILE "ms")
    else ()
        set(SHADER_PROFILE "cs")
    endif ()
    set(SPIRV "${PROJECT_BINARY_DIR}/shaders/${FILE_NAME}.spv")
    #    Check last shader model support here: https://github.com/KhronosGroup/Vulkan-Guide/blob/main/chapters/hlsl.adoc#shader-model-coverage
    add_custom_command(
            OUTPUT ${SPIRV}
            COMMAND ${CMAKE_COMMAND} -E make_directory "${PROJECT_BINARY_DIR}/shaders/"
            COMMAND ${DIRECTX_DXC_TOOL} -spirv -T ${SHADER_PROFILE}_6_6 -E main -fspv-target-env=vulkan1.3 ${HLSL} -Zi -fspv-debug=line -Fo ${SPIRV}
            DEPENDS ${HLSL_SOURCE_FILES})
    list(APPEND SPIRV_BINARY_FILES ${SPIRV})
endforeach (HLSL)

add_custom_command(TARGET plaxel POST_BUILD
        COMMAND ${CMAKE_COMMAND} -E make_directory "$<TARGET_FILE_DIR:plaxel>/shaders/"
        COMMAND ${CMAKE_COMMAND} -E copy_directory
        "${PROJECT_BINARY_DIR}/shaders"
        "$<TARGET_FILE_DIR:plaxel>/shaders"
)